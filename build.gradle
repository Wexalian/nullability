import java.time.Duration

buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

group = 'com.wexalian.nullability'
version = '0.0.3'

repositories {
    mavenCentral()
}

java {
     toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }

    withSourcesJar()
    withJavadocJar()
}

publishing { //todo see if possible to publish annotations separately
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'nullability'
            from components.java

            pom {
                name = 'nullability'
                packaging = 'jar'
                description = 'Nullability annotations and utilities'
                url = 'https://www.github.com/Wexalian/nullability'

                licenses {
                    license {
                        name = 'The Unlicense'
                        url = 'https://unlicense.org/'
                    }
                }
                developers {
                    developer {
                        id = 'Wexalian'
                        name = 'Lars Gerrits'
                        email = 'larsgerrits99@gmail.com'
//                        organization = 'wexalian'
//                        organizationUrl = 'http://www.wexalian.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Wexalian/nullability.git'
                    developerConnection = 'scm:git:ssh://github.com/Wexalian/nullability.git'
                    url = 'https://www.github.com/Wexalian/nullability'
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = uri("https://s01.oss.sonatype.org/service/local/")
            snapshotRepositoryUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            username = deployRepoUsername
            password = deployRepoPassword
        }
    }
    transitionCheckOptions {
        maxRetries.set(40) //40 tries of 3 seconds = 2 minutes
        delayBetween.set(Duration.ofMillis(3000))
    }
}

signing {
    sign publishing.publications.mavenJava
}
